'I hear the echoes'
-Rivers

I
-Phineas

A
-Keeling

E
-Mora

Me
-Celeste

'There lies on a wooden table, a silver key.',
'In the corner sits a half-rotted wardrobe, smelling of must.',
-Jeremey(1863)

'There is a wooden table on the verge of collapse. The key that sat on it is now in your pocket.',
'In the corner sits a half-rotted wardrobe, smelling of must.',
-Jeremey(1873)

'A sliver of light creeps into the box-like room through a tiny crack in the ceiling.'
-Jeremey(1900)

'This dingy chamber smells of dust and age. You appear to be deep, deep underground.'
-Celeste(1773)

'This long hallway slants sharply up into a steep climb, and a small trickle of water seeps down from somewhere above.'
-Gabrin(1972)

'A mysterious unknown symbol adorns the far wall, unlike anything you've seen. It hurts to stare at.',
'In one corner, a dirty straw mat marks where you woke up in this strange place.',
-Celeste(1768)

'At the end of the hall sturdy iron door, strongly secured to the stone walls, blocks your passage. It is clearly locked.',
'The dripping water has allowed moss to spread along one wall of this room. The smell of it makes you woozy.',
-Gabrin(1984)

'At the end of the hall the sturdy iron door lies unlocked and slightly ajar.',
'The dripping water has allowed moss to spread along one wall of this room. The smell of it makes you woozy.',
-Gabrin(1990)

WATER FALLS
water, 'move left', over red rock!
Oh when does your journey cease?
Celeste, is, under the run!
Does the water she takes miss the home it had?
Celeste, o, when!
When the water flows.
Celeste, run, away from sun!
Can you hear the water call to thee?
Celeste, lo, dive!
Let the falling water take you deeper.
Bring water taken back to home.
water, 'move right', under to home!
How far will the hidden flow go?
Celeste, no, the lure lies!
How can you know where the current may lead?
Celeste, why, stay!
For the water's home is not a home for thee.
Celeste, o, young and sad!
Did not curiosity kill the cat?
Celeste, lo, stay!
There are warmer homes for those like thee above the water's shine.
The water falls but nothing else.

WINGED FLIGHT
winged, begone, unbound skies!
What winds will carry me far and wide?
'You hear a distant rumbling somewhere deep in the walls.', so odd!
In skies so high you rarely hear the earth.
winged, to I, is life alone!
So why would the earth bear conscience to me?
'The room shakes ominously around you.', so odd!
Rooms and walls are no concerns of mine.
winged, oh flight, isn't so free!
No earthly ties to no earthly things?
'The room shakes violently on all sides!', I fear!
For moments now more vital, than flight away could ever be.

SUMMER FIREFLIES
summer, 'search room', mother smiles!
Doth thy ray ever cease?
Celeste, o, hide thy eyes!
For why hide in thy room?
roomItems, Jeremey(1863), stor!
end block.
Celeste, 01, is equivalent!
if?
roomItems, Celeste(1768), stor!
end block.
Celeste, 012, is equivalent!
if?
roomItems, Gabrin(1984), stor!
end block.
For...
itemList roomItems
itemList, prints!
end for.
end if.

SOFT CLOUDS
soft, 'get key', is equivalent!
if?
Celeste, 0, is equivalent!
if?
Keeling, 01, stor!
Jeremey(1863), Jeremey(1873), stor!
end if.
end if.
soft, 'unlock door', is equivalent!
if?
Celeste, 012, is equivalent!
Keeling, this is an anding!
if?
Mora, 01, stor!
Gabrin(1984), Gabrin(1990), stor!
end if.
end if.
soft, 'escape', is equivalent!
if?
Celeste, 012, is equivalent!
Mora, this is an anding!
if?
Phineas, 01, stor!
end if.
end if.

DAWN BAGEL
dawn, 0, is equivalent!
thy sun burns?
Jeremey(1900), prints!
end if.
dawn, 01, is equivalent!
if?
Celeste(1773), prints!
end if.
dawn, 012, is equivalent!
if?
Gabrin(1972), prints!
end if.

For...
index value Rivers
Phineas, negate this word!
if?
"Celeste, nonsense" (Dawn Bagel)
0123456!
playerInput, cache!
"playerInput, nonsense" (Water Falls)
"index, nonsense" (Winged Flight)
"playerInput, nonsense" (Summer Fireflies)
"playerInput, nonsense" (Soft Clouds)
End block.
end if.

Phineas, stor!
if?
'You stumble out into the light and the open sun almost blinds you. You've escaped!', prints!
'Turning around you watch as the strange cave collapses in on itself in a cloud of dust and rubble.', prints!
end block.
Phineas, negate this word!
if?
'The walls crack and break around you. Rocks fall and you are entombed forever in this strange place.', prints!
end block.